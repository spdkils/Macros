#Turbo

|- This script expects you to execute it passing 2 parameters 1: Who the boss is, 2: Who to attack.
|- --------------------------------------------------------------${Param0}           ${Param1}
|- Example /mac simple ${Me.ID} ${Target.ID}
|- If you want to use this via eqbc then it could look like this. /bct Eemo //mac simple ${Me.ID} ${Target.ID}
|- Eemo will get this as a command.  /mac simple 534 292 (Just example numbers, depends on target, and zone)

#EVENT mobCast "#1# begins to cast a spell."
#EVENT hateList "- name: #1#, damage: #2#, hate: #3#"


Sub Main
/echo Lame Assist
/declare intMyHate int outer 0
/declare intTopHate int outer 0
/declare strTopName string outer NULL
/declare aids bool outer true
/declare myTarget int local ${Param1}
/target id ${myTarget}
:WAITFORTARGET
/delay 1
/if (${Target.ID} != ${myTarget} && ${Spawn[id ${myTarget}].ID} && !${Spawn[id ${myTarget}].Dead}) /goto :WAITFORTARGET
/if (!${Target.ID}) /return
/face fast
/if (${Spawn[id ${myTarget}].LineOfSight}) /stick 9 id ${myTarget} loose
/if (${Target.Type.Equal[PC]}) {
	/attack off
	/return
 }
/bc Attacking ${Target.Name}
/declare caster bool local ${Target.Class.PureCaster}
/if (!${caster}) /varset caster ${Target.Class.PetClass}
:TOP
	/if (${Me.PctHPs} > 30) /varset aids true
	/attack on
	/doevents
	/face fast id ${myTarget}
	/if (${intTopHate} > 0) {
		/bc ${strTopName} : ${intTopHate}
		/varset intTopHate 0
	}
	/if (${Target.Type.Equal[NPC]}) {
		/if (${Target.Distance}<15 && ${Me.CombatState.Equal[Combat]} && ${Me.AbilityReady[Taunt]}) /doability "Taunt"
		/if (${Target.Distance}<15 && ${Me.Combat} && ${Me.AbilityReady[Bash]} && !${caster}) /doability "Bash"
		/if (${Me.Combat} && ${Me.SpellReady[Flash of Light]} && ${SpellTimer[${myTarget},Flash of Light]} < 4) /casting "Flash of Light" -targetid|${myTarget}
		/if (${Me.Combat} && ${Me.SpellReady[Desist]} && ${Me.TargetOfTarget.ID} != ${Me.ID} && !${Me.AbilityReady[Taunt]}) {
			/if (${SpellTimer[${myTarget},Flash of Light]}) {
				/keypress forward hold
				/delay 1
				/keypress forward
			}
			/face fast id ${myTarget}
			/varset intTopHate 0
			/say #hatelist
			/casting "Desist" -targetid|${myTarget}
			/delay 4s !${Me.Casting.ID}
			}
		/if (${Me.Combat} && ${Me.SpellReady[Cease]} && ${Me.TargetOfTarget.ID} != ${Me.ID} && !${Me.AbilityReady[Taunt]}) /casting "Cease" -targetid|${myTarget}
		/if (${Me.Combat} && ${Me.SpellReady[Stun]} && ${Me.TargetOfTarget.ID} != ${Me.ID} && !${Me.AbilityReady[Taunt]}) /casting "Stun" -targetid|${myTarget}
		/if (${Me.Casting.ID}) /bc I will not be ignored!
		/delay 4s !${Me.Casting.ID}
	}
	/if (${Me.PctHPs} < 31) {
		/bc BAD NEWS!
		/if (${aids})	/vgroup 007
		/varset aids false
		/if (${Cast.Ready[Lay on Hands]}) {
			/casting "lay on hands" alt -targetid|${Me.ID}
			/delay 4 !${Me.Casting.ID}
			/target id ${myTarget}
		}
		
	}
	
	/if (${Spawn[id ${Param0}].PctHPs}<30 && ${Me.SpellReady[Healing]}) {
		/stick off
		/casting "Healing" -targetid|${Param0}
		/delay 4s !${Me.Casting.ID}
		/target id ${myTarget}
		/stick 9 id ${myTarget} loose
	}
	/delay 2
	/if (!${Spawn[id ${myTarget}].Dead} && ${Spawn[id ${myTarget}].ID}) /GOTO :TOP
	/attack off
	/stick 7 id ${Param0} loose
/return
| endsub

Sub EVENT_mobCast(strFullmsg,strWho)
	/if (${Target.CleanName.Equal[${strWho}]} && ${Target.Distance}<15 && ${Me.Combat} && ${Me.AbilityReady[Bash]}) {
		/doability "Bash"
		/bc Shield to the FACE
		/return
	}
	/if (${Target.CleanName.Equal[${strWho}]} && ${Me.SpellReady[Cease]} && !${Me.AbilityReady[Bash]}) {
		/cast "Cease"
		/bc No CAST!
		/delay 4s !${Me.Casting.ID}
		/return
	}
	/if (${Target.CleanName.Equal[${strWho}]} && ${Me.SpellReady[Desist]} && !${Me.AbilityReady[Bash]}) {
		/cast "Desist"
		/bc No CAST!
		/delay 4s !${Me.Casting.ID}
		${Group.Member[1].Name}
		/return
	}
/return

sub EVENT_hateList(string strFullmsg,string strWho,int intDmg, int intHate)
	/if (${intHate}>${intTopHate}) {
		/varset intTopHate ${intHate}
		/varset strTopName ${strWho}
		}
	/if (${Me.Name.Equal[${strWho}]}) /bc My hate == ${intHate}
/return
